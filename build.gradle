buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        )
    }
}

plugins {
    id 'java'
}

apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'rip.deadcode'
version '0.2-SNAPSHOT'

sourceCompatibility = 1.8
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
    maven {
        url = "https://dl.bintray.com/minebreaker/test"
    }
}

dependencies {
    compile(
            'io.ratpack:ratpack-core:1.5.4',

//            'com.google.guava:guava:25.1-jre',
            'com.google.code.findbugs:jsr305:3.0.2',

            'io.sentry:sentry:1.7.5',
    )
    testCompile(
            'org.junit.jupiter:junit-jupiter-api:5.2.0',

            'io.ratpack:ratpack-test:1.5.4',

            'com.google.truth:truth:0.41',
            'com.google.truth.extensions:truth-java8-extension:0.41',
            'org.mockito:mockito-core:2.18.3',
            'rip.deadcode:izvestia:0.2',
//            'com.google.jimfs:jimfs:1.1'

            'org.slf4j:slf4j-api:1.7.25',
            'org.slf4j:jul-to-slf4j:1.7.25', // Guava uses java.util.logging
            'ch.qos.logback:logback-classic:1.2.3',
            'io.sentry:sentry-logback:1.7.5',
    )
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    pkg {
        repo = 'test'
        name = 'ratpack-sentry'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/minebreaker-tf/ratpack-sentry.git'
    }
    publications = ['bintrayPublication']
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
